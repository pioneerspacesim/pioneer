#
# Winding is always CCW. DEVIATION SHOULD BE CONSIDERED A BUG!!11
#
# To get an idea of scale, a double-decker bus is 11m long and a
# boeing 747 is 71m long

model(wind_turbine_blade, scale=1.0, radius=1.0) {
	lod { full, 5px }
	objlod a = { 8, 4 }
	cylinder(a, v(0,0,0), v(0,0,.1), v(0,1,0), radius=0.065)
	tri(v(-.065, 0, 0.03), v(.065, 0, 0.07), v(0, 1, 0.05))
	tri(v(0.0325, 0.056, 0.03), v(-.0325, -.056, 0.07), v(0.87, -0.5, 0.05))
	tri(v(0.0325, -.056, 0.03), v(-.0325, 0.056, 0.07), v(-.87, -0.5, 0.05))
	# back faces
	tri(v(.065, 0, 0.07), v(-.065, 0, 0.03), v(0, 1, 0.05))
	tri(v(-.0325, -.056, 0.07), v(0.0325, 0.056, 0.03), v(0.87, -0.5, 0.05))
	tri(v(-.0325, 0.056, 0.07), v(0.0325, -.056, 0.03), v(-.87, -0.5, 0.05))
}

model(wind_turbine1, scale=2.0, radius=10.0) {
	lod { full, 5px, 2px }
	objlod a = { 8, 4, 4 }
	material(1,1,1, 0,0,0, 0, 0,0,0)
	cylinder(a, v(0,0,0), v(0,20,0), v(0,0,1), radius=0.6)
	lod1:lod2:cylinder(a, v(0,20,1), v(0,20,-1), v(0,1,0), radius=0.7)
	minhand = vrotate(v(0,0,1), v(1,0,0), animfn=lin4sec)
	lod1:lod2:subobject(wind_turbine_blade, v(0,20,1), minhand, v(0,0,1), scale=12.0)
}
model(wind_turbine2, scale=2.0, radius=10.0) {
	lod { full, 5px, 2px }
	objlod a = { 8, 4, 4 }
	material(1,1,1, 0,0,0, 0, 0,0,0)
	cylinder(a, v(0,0,0), v(0,20,0), v(0,0,1), radius=0.6)
	lod1:lod2:cylinder(a, v(0,20,1), v(0,20,-1), v(0,1,0), radius=0.7)
	minhand = vrotate(v(0,0,1), n(1,-1,0), animfn=lin4sec)
	lod1:lod2:subobject(wind_turbine_blade, v(0,20,1), minhand, v(0,0,1), scale=12.0)
}
model(wind_turbine3, scale=2.0, radius=10.0) {
	lod { full, 5px, 2px }
	objlod a = { 8, 4, 4 }
	material(1,1,1, 0,0,0, 0, 0,0,0)
	cylinder(a, v(0,0,0), v(0,20,0), v(0,0,1), radius=0.6)
	lod1:lod2:cylinder(a, v(0,20,1), v(0,20,-1), v(0,1,0), radius=0.7)
	minhand = vcross(v(0,0,1), n(-1,-1,0))
	lod1:lod2:subobject(wind_turbine_blade, v(0,20,1), minhand, v(0,0,1), scale=12.0)
}

model(skyscraper1, scale=1.0, radius=300.0) {
	# Inset black bit
	# h g
	#
	# e f
	# d c
	# a b
	a = v(-20,0,10)
	b = v(20,0,10)
	c = v(20,40,10)
	d = v(-20,40,10)
	e = v(-20,48,10)
	f = v(20,48,10)
	g = v(20,200,10)
	h = v(-20,200,10)
	material(.2,.2,.2, .7,.7,.7, 10, 0,0,0)
	quad(a,b,c,d)
	quad(e,f,g,h)
	# Outset
	# h2 g2
	#
	# e2 f2
	# d2 c2
	# a2 b2
	a2 = v(-20,0,15)
	b2 = v(20,0,15)
	c2 = v(20,40,15)
	d2 = v(-20,40,15)
	e2 = v(-20,48,15)
	f2 = v(20,48,15)
	g2 = v(20,200,15)
	h2 = v(-20,200,15)
	material(.9,.9,.9, 0,0,0, 0, 0,0,0)
	# inset bits
	quad(b,b2,c2,c)
	quad(d,c,c2,d2)
	quad(a,d,d2,a2)
	quad(f,e,e2,f2)
	quad(g,f,f2,g2)
	quad(h,g,g2,h2)
	quad(e,h,h2,e2)
	# front face
	# fd fc
	# fa fb
	fa = v(-30,0,15)
	fb = v(30,0,15)
	fc = v(30,210,15)
	fd = v(-30,210,15)
	quad(d2,c2,f2,e2)
	quad(fc,fd,h2,g2)
	
	xref:flat(0, v(0,0,1),
		line(g2),
		line(f2),
		line(c2),
		line(b2),
		line(fb),
		line(fc))

	top = v(0,260,-40)
	# rear
	xref:quad(fc,fb,v(0,0,-40),v(0,210,-40))
	xref:tri(fc,v(0,210,-40),top)
	# spire
	spire = v(0,300,-40)
	xref:tri(v(1.5,257.5,-37.25),
		top,spire)
	tri(v(-1.5,257.5,-37.25),v(1.5,257.5,-37.25),
		spire)
	# front roof triangle section
	#      top
	#      tc
	#
	#   ta   tb
	# fd       fc
	ta = v(-21, 215, 9.5)
	tb = v(21, 215, 9.5)
	tc = v(0, 250, -29)
	quad(fd,fc,tb,ta)
	xref:quad(fc,top,tc,tb)
	material(.2,.2,.2, .7,.7,.7, 10, 0,0,0)
	tri(ta,tb,tc)


}

model(box, scale=1.0, radius=10.0) {
	material(.5, .5, .5, 0,0,0, 2.0, 0, 0, 0)
	extrusion(v(0,0,-7.5), v(0,0,7.5), n(0,1,0), radius=6.0, {
		v(-1,-1,0), v(1,-1,0), v(1,1,0), v(-1,1,0)
	})
}

model(building1, scale=1.0, radius=10.0) {
	material(.5, .5, .5, 0,0,0, 2.0, 0, 0, 0)
	extrusion(v(0,0,-7.5), v(0,0,7.5), n(0,1,0), radius=6.0, {
		v(-1,0,0), v(1,0,0), v(1,1,0), v(-1,1,0)
	})
}

model(building2, scale=1.0, radius=20.0) {
	material(.5, .5, .5, 0,0,0, 2.0, 0, 0, 0)
	extrusion(v(0,0,-16), v(0,0,16), n(0,1,0), radius=1.0, {
		v(-16,0,0), v(16,0,0), v(16,20,0), v(-16,20,0)
	})
}

# 64x64m is rough dimensions of WTC base
model(building3, scale=1.0, radius=40.0) {
	material(.5, .5, .5, 0,0,0, 2.0, 0, 0, 0)
	extrusion(v(0,0,-32), v(0,0,32), n(0,1,0), radius=1.0, {
		v(-32,0,0), v(32,0,0), v(32,10,0), v(-32,10,0)
	})
}

model(skyscraper2, scale=1.0, radius=210.0) {
	material(.5, .5, .5, 0,0,0, 2.0, 0, 0, 0)
	extrusion(v(0,0,-32), v(0,0,32), n(0,1,0), radius=1.0, {
		v(-32,0,0), v(32,0,0), v(32,100,0), v(-32,100,0)
	})
	extrusion(v(0,0,-27), v(0,0,27), n(0,1,0), radius=1.0, {
		v(-27,110,0), v(27,110,0), v(27,210,0), v(-27,210,0)
	})
	extrusion(v(0,0,-22), v(0,0,22), n(0,1,0), radius=1.0, {
		v(-22,220,0), v(22,220,0), v(22,325,0), v(-22,325,0)
	})
	extrusion(v(0,0,-17), v(0,0,17), n(0,1,0), radius=1.0, {
		v(-17,335,0), v(17,335,0), v(17,440,0), v(-17,440,0)
	})
	# dark segment separators
	material(.3, .3, .3, 0,0,0, 2.0, 0, 0, 0)
	extrusion(v(0,0,-32), v(0,0,32), n(0,1,0), radius=1.0, {
		v(-32,100,0), v(32,100,0), v(32,110,0), v(-32,110,0)
	})
	extrusion(v(0,0,-27), v(0,0,27), n(0,1,0), radius=1.0, {
		v(-27,210,0), v(27,210,0), v(27,220,0), v(-27,220,0)
	})
	extrusion(v(0,0,-22), v(0,0,22), n(0,1,0), radius=1.0, {
		v(-22,325,0), v(22,325,0), v(22,335,0), v(-22,335,0)
	})
}

model(factory1, scale=1.0, radius=30.0) {
	lod { full, 10px, 2px }
	objlod a = { 16, 8, 4 }
	material(.5, .5, .3, 0,0,0, 2.0, 0, 0, 0)
	extrusion(v(0,0,-20), v(0,0,20), n(0,1,0), radius=1.0, {
		v(-20,0,0), v(20,0,0), v(20,20,0), v(-20,20,0)
	})
	extrusion(v(0,0,-20), v(0,0,20), n(0,1,0), radius=1.0, {
		v(-20,20,0), v(0,20,0), v(0,28,0), v(-2,30,0), v(-18,30,0),
		v(-20,28,0)
	})
	material(.6, .6, .6, 0,0,0, 2.0, 0, 0, 0)
	cylinder(a, v(-10,30,10), v(-10,50,10), v(0,0,1), radius=2.0)
	cylinder(a, v(-10,30,0), v(-10,50,0), v(0,0,1), radius=2.0)
	cylinder(a, v(-10,30,-10), v(-10,50,-10), v(0,0,1), radius=2.0)

}

model(clockhand, scale=1.0, radius=1.0) {
	material(0,0,1, .7,.7,1, 10, 0,0,0)
	tri(v(-0.06, -0.06, 0), v(0.06, -0.06, 0), v(0, 1, 0))
}

model(clock, scale=1.0, radius=1.0) {
	lod { full, 15px }
	material(1,1,1, 0,0,0, 0, 0,0,0)
	circle(12, v(0,0,0), v(0,0,1), v(0,1,0), radius=1.0)
	zbias(v(0,0,0), v(0,0,1), 2)

	material(.5,.5,0, 1,1,0, 10, 0,0,0)
	lod1:text(global:21, v(0,0.85,0), v(0,0,1), v(1,0,0), scale=0.15)
	lod1:text(global:10, v(0.425,0.74,0), v(0,0,1), v(1,0,0), scale=0.15)
	lod1:text(global:11, v(0.74,0.43,0), v(0,0,1), v(1,0,0), scale=0.15)
	lod1:text(global:12, v(0.85,0,0), v(0,0,1), v(1,0,0), scale=0.15)
	lod1:text(global:13, v(0.74,-.425,0), v(0,0,1), v(1,0,0), scale=0.15)
	lod1:text(global:14, v(0.425,-.74,0), v(0,0,1), v(1,0,0), scale=0.15)
	lod1:text(global:15, v(0,-.85,0), v(0,0,1), v(1,0,0), scale=0.15)
	lod1:text(global:16, v(-.425,-.736,0), v(0,0,1), v(1,0,0), scale=0.15)
	lod1:text(global:17, v(-.74,-.425,0), v(0,0,1), v(1,0,0), scale=0.15)
	lod1:text(global:18, v(-.85,0,0), v(0,0,1), v(1,0,0), scale=0.15)
	lod1:text(global:19, v(-.74,.425,0), v(0,0,1), v(1,0,0), scale=0.15)
	lod1:text(global:20, v(-.425,.736,0), v(0,0,1), v(1,0,0), scale=0.15)
	
	minhand = vrotate(v(0,0,1), v(1,0,0), animfn=hour)
	hourhand = vrotate(v(0,0,1), v(1,0,0), animfn=halfday)

	zbias(v(0,0,0), v(0,0,1), 2)
	subobject(clockhand, v(0,0,0), minhand, v(0,0,1), scale=0.65)
	zbias(v(0,0,0), v(0,0,1), 2)
	subobject(clockhand, v(0,0,0), hourhand, v(0,0,1), scale=0.5)
}

model(church, scale=1.0, radius=22.0) {
	lod { full, 10px }
	material(.5, .5, .3, 0,0,0, 2.0, 0, 0, 0)
	extrusion(v(0,0,-18), v(0,0,12), n(0,1,0), radius=1.0, {
		v(-7,0,0), v(7,0,0), v(7,10,0), v(-7,10,0)
	})
	extrusion(v(0,0,-18), v(0,0,-4), n(0,1,0), radius=1.0, {
		v(-7,10,0), v(7,10,0), v(7,25,0), v(-7,25,0)
	})
	roof1 = v(0,20,12)
	roof2 = v(0,20,-4)
	spire = v(0,45,-11)
	tri(v(-7,10,12), v(7,10,12), roof1)
	material(.8, .3, .0, 0,0,0, 2.0, 0, 0, 0)
	xref:quad(roof2, roof1, v(7,10,12), v(7,10,-4))
	xref:tri(spire, v(7,25,-4), v(7,25,-18))
	tri(spire, v(-7,25,-4), v(7,25,-4))
	tri(spire, v(7,25,-18), v(-7,25,-18))

	clockpos1 = v(-7,18,-11)
	clockpos2 = v(7,18,-11)
	
	lod1:zbias(clockpos1, v(-1,0,0), 1)
	lod1:subobject(clock, clockpos1, v(0,1,0), v(-1,0,0), scale=2.5)
	lod1:zbias(clockpos2, v(1,0,0), 1)
	lod1:subobject(clock, clockpos2, v(0,1,0), v(1,0,0), scale=2.5)
	nozbias()
#subobject(object_name, vector pos, vector up, vector zaxis [, scale=float, animfn=])
}

model(nosewheel, scale=1.0, radius=7.0) {
	lod { full, 4px }
	objlod a = { 8, 4 }
	v6 = v(0, 0, 0)
	v7 = v(0, 3, 0)
	v8 = v(0, 5, 0)
	material(.5, .5, .5, .5, .5, .5, 2.0, 0, 0, 0)
	cylinder(a, v6, v8, v(0,0,1), radius=.4)
	cylinder(a, v7, v8, v(0,0,1), radius=.5)
	material(.3, .3, .3, 0,0,0, 1, 0, 0, 0)
	xref:cylinder(a, v(.5,5,0), v(1,5,0), v(0,0,1), radius=1.0)
}

model(nosewheelunit, scale=1.0, radius=7.0) {
	lod { full }
	v6 = v(1.5, 0, 6) # flaps
	v7 = v(1.5, 0, -1)
	v8 = v(0, 0, 6)
	v9 = v(0, 0, -1)
	v10 = v(1.5, 1.5, 6)
	v11 = v(1.5, 1.5, -1)
	tan1 = v(0, 1.5, 0) # tangents
	tan2 = v(-1.5, 0, 0)

	v20 = vhermite(v8, v10, tan1, tan2, animfn=gflap)
	v21 = vhermite(v9, v11, tan1, tan2, animfn=gflap)
	v22 = vlinear(v(0,0,1), v(0,1,0), animfn=gear)
	v23 = vcross(v(1,0,0), v22)

	material(.2,.2,.2, 0,0,0, 1, 0,0,0)
	xref:quad(v8, v6, v7, v9) # flap internal
	material(2)
	xref:quad(v6, v20, v21, v7)
	xref:quad(v6, v7, v21, v20)

	subobject(nosewheel, v(0,0,0), v22, v23, scale=1.0)
}

model(mainwheel, scale=1, radius=8) {
	lod { full, 50px, 10px }
	objlod a = { 16, 8, 4 }
	v6 = v(0,0,0)
	v7 = v(0,3,0)
	v8 = v(0,5,0)
	v13 = v(0, 5, 1.4) # crossbar
	v14 = v(0, 5, -1.4)

	material(.5,.5,.5, 1,1,1, 2, 0,0,0)
	cylinder(a, v6, v8, v(0,0,1), radius=.4)
	cylinder(a, v7, v8, v(0,0,1), radius=.5)
	cylinder(4, v13, v14, v(1,0,0), radius=.5)
	material(.3,.3,.3, 0,0,0, 1, 0,0,0)
	xref:cylinder(a, v(.5, 5, 1.1), v(1, 5, 1.1), v(0,0,1), radius=1)
	xref:cylinder(a, v(.5, 5, -1.1), v(1, 5, -1.1), v(0,0,1), radius=1)
}


model(mainwheelunit, scale=1.0, radius=8.0) {
	lod { full }
	v6 = v(1.5, 0, 6) # flap
	v7 = v(1.5, 0, -1)
	v8 = v(0, 0, 6)
	v9 = v(0, 0, -1)
	v10 = v(1.5, 1.5, 6)
	v11 = v(1.5, 1.5, -1)
	tan1 = v(0, 1.5, 0)
	tan2 = v(-1.5, 0, 0)
	v20 = vhermite(v8, v10, tan1, tan2, animfn=gflap)
	v21 = vhermite(v9, v11, tan1, tan2, animfn=gflap)
	v22 = vlinear(v(0,0,1), v(0,1,0), animfn=gear)
	v23 = vcross(v(1,0,0), v22)

	material(.2,.2,.2, 0,0,0, 1, 0,0,0)
	xref:quad(v8, v6, v7, v9) # flap internal
	material(2)
	xref:quad(v6, v20, v21, v7)
	xref:quad(v6, v7, v21, v20)
	subobject(mainwheel, v(0,0,0), v22, v23, scale=1.0)
}

# Ship 
model(taipan, scale=1.0, radius=60.0) {
	lod { full, 40px, 20px, 5px }
	objlod slod_a = { 8, 4, 2, 1 }
	objlod slod_tube = { 16, 10, 8, 4 }
	
	# right bot/top of +z extrusion end
	box_bot = v(15,-5,50)
	box_top = v(15,5,50)
	box_bot_flange = v(16,-5,51)
	box_top_flange = v(16,5,51)
	wingtip = v(40,-3,40)
	front_wingtip = v(15,-5,-30)
	material(.3,.3,.3, .6,.6,.6, 100, 0,0,0)
	# rear
	quad(box_top, v(-15,5,50), v(-15,-5,50), box_bot)
	# arse flange
	xref:quad(
		box_bot,
		box_bot_flange,
		box_top_flange,
		box_top
	)
	# arse curved bits
	xref:smooth(slod_a, 
		line(wingtip),
		hermite(box_top_flange, v(-50,0,20), v(50,-30,-20)), # herm 1
		line(box_bot_flange))
	# top of wing
	xref:smooth(slod_a, 
		hermite(wingtip, v(50,-30,-20), v(-50,0,20)), # herm 1
		hermite(front_wingtip, v(0,0,-80), v(40,0,40)), # herm 2 leading edge
#			line(v(15, 5, -30)),
		line(box_top),
		line(box_top_flange))
	# bottom of wing
	xref:flat(slod_a, v(0,-1,0),
		line(box_bot),
		line(front_wingtip),
		hermite(wingtip, v(40,0,40), v(0,0,-80)), # herm3 leading edge lower
		line(box_bot_flange))
	# inner wing bit
	xref:tri(front_wingtip, box_bot, box_top)

	# top and bottom at rear
	quad(box_top, v(15,4.3,45), v(-15,4.3,45), v(-15,5,50))
	quad(box_bot, v(-15,-5,50), v(-15,-5,35), v(15,-5,35))
	quad(v(15,-5,35), v(-15,-5,35), v(-15,1,30), v(15,1,30))

	tri(v(-15,4.3,45), v(15,4.3,45), v(0, 3.5, 35))
#		xref:quad(v(0,4,35), v(15,5,45), v(15,4,35), v(0,3.5,25))

	# make materials be assignable
	material(.3,0,0, .7,0,0, 100, 0,0,0)
	xref:tri(v(0,3.5,35),v(15,4.3,45), v(15,3,35))
	material(.3,.3,.3, .6,.6,.6, 100, 0,0,0)
	# top body bit
	tanTipR = v(20,10,50)
	tanTipL = v(-20,10,50)
	tanFwd = v(0,-10,-50)
	smooth(slod_a, 
		line(v(15,3,35)),
		hermite(v(0,-2,0), tanFwd, tanTipR),
		hermite(v(-15,3,35), tanTipL, tanFwd),
		line(v(0,3.5,35)))
	
	smooth(slod_a,
		hermite(v(0,-2,0), tanFwd, tanTipL),
		hermite(v(15,3,35), tanTipR, tanFwd),
		hermite(v(-15,3,35), v(0,-30,0), v(0,-30,0)))

	xref:smooth(slod_a, 
		hermite(v(15,-5,-10), v(-10,0,0), v(-10,0,0)),
		line(v(15,-5,50)),
		hermite(v(15,5,50), v(-10,0,0), v(-10,0,0)),
		line(v(15,-2.5,-10)))

	# engines
	material(.1,.1,.1, .7,.7,.7, 150.0, 0,0,0)
	
	# front
	# rear
	tube(slod_tube, v(0,0,50), v(0,0,52), v(0,1,0), innerrad=3.5, outerrad=4)
	xref:tube(slod_tube, v(10,0,50), v(10,0,52), v(0,1,0), innerrad=3.5, outerrad=4)
	material(animfn=thrustpulse,
		material(0,0,0, 0,0,0, 0, .2,.2,1),
		material(0,0,0, 0,0,0, 0, .7,.7,1))
	# front
	xref:flat(slod_a, v(0,0,-1),
		line(v(15,-5,-10)),
		hermite(v(15,-2.5,-10), v(-10,0,0), v(-10,0,0)))
	#rear
	zbias(v(0,0,50), v(0,0,1), 1)
	circle(slod_tube, v(0,0,50), v(0,0,1), v(0,1,0), radius=3.5)
	xref:circle(slod_tube, v(10,0,50), v(0,0,1), v(0,1,0), radius=3.5)
#	xref:circle(slod_tube, v(10,0,3), v(0,0,-1), v(0,1,0), radius=2.5)
	nozbias()

	# license plate
	material(.9,.9,.9, 0,0,0, 0, 0,0,0)
	idtext_pos = v(0, 4.65, 47.5)
	zbias(idtext_pos, v(0,0,1), 1)
	lod1:text(params:0, idtext_pos,
		vnormal(
		v(-15,4.3,45),
		v(-15,5,50),
		box_top
		),
		v(1,0,0), scale=3)
	
	xref:notangular:thruster(fwd, v(10,0,50), 50.0)
	notangular:thruster(fwd, v(0,0,50), 50.0)
	xref:notangular:thruster(rev, v(14,-3.75,-10), 25.0)
	
	xref:thruster(down, v(18, 5, 45), 10.0)
	xref:thruster(down, v(18, -4, -20), 10.0)
	xref:thruster(up, v(18, -5, 45), 10.0)
	xref:thruster(up, v(18, -5, -20), 10.0)

	thruster(left, v(18, 0, 50), 15.0)
	thruster(right, v(-18, 0, 50), 15.0)
	
	subobject(nosewheelunit, v(22,-5,0), v(0,-1,0), v(0,0,1), scale=1.0, onflag=gear)
	subobject(nosewheelunit, v(-22,-5,0), v(0,-1,0), v(0,0,1), scale=1.0, onflag=gear)
	subobject(mainwheelunit, v(12,-5,40), v(0,-1,0), v(0,0,1), scale=1.0, onflag=gear)
	subobject(mainwheelunit, v(-12,-5,40), v(0,-1,0), v(0,0,1), scale=1.0, onflag=gear)
}

model(hermite_wank, scale=1, radius=1) {
	# 1/8th sphere
	v6 = v(0.0, 5.0, 0.0)
	v7 = v(-5.0, 0.0, 0.0 )
	v8 = v(0.0, 0.0, 5.0 )

	v9 = v(-7.0, 0.0, 0.0 )			
	v10 = v(0.0, 7.0, 0.0 )
	v11 = v(0.0, 0.0, 7.0 )			
	material(.2,.0,.2, 1,0,1, 10, 0,0,0)
	xref:smooth(8, 
		hermite(v7, v9, v10),
		hermite(v8, v11, v9),
		hermite(v6, v10, v11))

	material(1, 0, 1, 0, 0, 0, 1, 0, 0, 0)
	extrusion(v(0,0,0), v(0,0,10), n(0,-1,0), radius=1.0,
		{
			v(-3.0, 0.0, 0),
			v(-2.0, -2.0, 0),
			v(2.0, -2.0, 0),			
			v(6.0, 0.0, 0),			
			v(2.0, 2.0, 0),
			v(-2.0, 2.0, 0)
		})
	xref:notangular:thruster(fwd, v(10,0,0), 30.0)
	xref:thruster(rev, v(10,0,0), 10.0)
	xref:thruster(up, v(10,0,0), 20.0)
	xref:thruster(down, v(10,0,0), 20.0)
	thruster(left, v(0,0,0), 20.0)
	thruster(right, v(0,0,0), 20.0)
}

model(junk, scale=1.5, radius=1.0) {
	lod { full, 5px, 2.5px }
	objlod arse = { 16, 10, 8 }
	
	material(.5,.5,.5,0,0,0, 0, 0,0,0)
	geomflag(10)
	xref:tube(arse, v(15,-20,0), vlinear(v(15,-15,0), v(15,-10,0), animfn=gear), v(0,0,1), innerrad=5.0, outerrad=10.0)
	quad(v(1,-2,0), v(1,-1,0), v(0,-1,0), v(0,-2,0))
	circle(arse, v(15,0,0), v(0,0,1), v(0,1,0), radius=10.0)
	xref:cylinder(arse, v(15,10,0), v(15,20,0), v(0,0,1), radius=10.0)

	subobject(hermite_wank, v(0,0,0), v(0,1,0), v(0,0,1), scale=1.0)
	subobject(hermite_wank, v(0,0,0), v(0,-1,0), v(0,0,1), scale=1.0, onflag=gear)
}

model(cargo, scale=1.0, radius=1.0) {
	lod { full, 10px, 5px }
	objlod cyl_lod = { 16, 16, 8 }

	top = v(0,1,0)
	bottom = v(0,-1,0)
	textpos = v(0,0.8,0)

	material(.5,.5,.5, 0,0,0, 0, 0,0,0)
	lod2:lod3:cylinder(cyl_lod, top, bottom, v(1,0,0), radius=1.0)
	
	lod1:tube(16, top, bottom, v(1,0,0), innerrad=0.9, outerrad=1.0)
	lod1:cylinder(16, textpos, v(0,-0.8,0), v(1,0,0), radius=0.9)
	lod1:material(1,0,0, 0,0,0, 0, 0,0,0)
	lod1:zbias(textpos, v(0,1,0), 1)
	lod1:text(params:0, textpos, v(0,1,0), v(1,0,0), scale=0.1)
}

model(spacestation_door, scale=1.0, radius=200.0) {
	a = v(-100,0,50)
	b = v(100,0,50)
	c = v(100,0,-50)
	d = v(-100,0,-50)
	quad(a,b,c,d)
	quad(d,c,b,a)
	material(.8,.8,0, 0,0,0, 0, 0,0,0)
	zbias(v(0,0,0), v(0,1,0), 1)
	# diagonal stripes on front
	quad(v(-10,0,-20), v(-30,0,-20), v(-70,0,20), v(-50,0,20))
	quad(v(30,0,-20), v(10,0,-20), v(-30,0,20), v(-10,0,20))
	quad(v(70,0,-20), v(50,0,-20), v(10,0,20), v(30,0,20))
	# on back
	zbias(v(0,0,0), v(0,-1,0), 1)
	quad(v(10,0,-20), v(30,0,-20), v(70,0,20), v(50,0,20))
	quad(v(-30,0,-20), v(-10,0,-20), v(30,0,20), v(10,0,20))
	quad(v(-70,0,-20), v(-50,0,-20), v(-10,0,20), v(-30,0,20))
	nozbias()
}	

# final resting place of docked ship
model(spacestation_entry1_stage3, scale=1.0, radius=1.0) {
	# mark as non-colliding (all >= 0x8000 is)
	geomflag(0x9000)
	a = v(-100,200,50)
	b = v(100,200,50)
	c = v(100,200,-50)
	d = v(-100,200,-50)
	a2 = v(-100,0,50)
	b2 = v(100,0,50)
	c2 = v(100,0,-50)
	d2 = v(-100,0,-50)
	material(.8,.8,.8, 0,0,0, 0, 0,0,0)
	quad(a,b,b2,a2)
	material(.8,0,.8, 0,0,0, 0, 0,0,0)
	quad(c,d,d2,c2)
	quad(d,c,b,a)
	xref:quad(b,c,c2,b2)
	material(1,1,1, 0,0,0, 0, 0,0,0)
	
	zbias(v(0,200,0), v(0,-1,0), 1)
	text(global:0, v(0,200,0), v(0,-1,0), v(-1,0,0), scale=10.0)
	nozbias()

	geomflag(0x8020)
	invisible:tri(v(0,100,0), v(0,0,0), v(0,0,0))
	invisible:tri(v(0,0,0), v(-1,0,0), v(0,0,-1))
	geomflag(0)
}

model(spacestation_entry1_stage2, scale=1.0, radius=1.0) {
	a = v(-100,0,50)
	b = v(100,0,50)
	c = v(100,0,-50)
	d = v(-100,0,-50)
	a2 = v(-100,-200,50)
	b2 = v(100,-200,50)
	c2 = v(100,-200,-50)
	d2 = v(-100,-200,-50)
	material(.8,.8,.8, 0,0,0, 0, 0,0,0)
	quad(a,b,b2,a2)
	quad(c,d,d2,c2)
	quad(a2,b2,c2,d2)
	material(.8,.8,.5, 0,0,0, 0, 0,0,0)
	xref:quad(b,c,c2,b2)
	geomflag(0x8010)
	invisible:tri(v(0,-100,0), v(0,0,0), v(0,0,0))
	invisible:tri(v(0,0,0), v(-1,0,0), v(0,0,-1))
	geomflag(0)
	
	material(.8,0,0, 0,0,0, 0, 0,0,0)
	door2 = vlinear(v(0,0,0), v(0,0,-100), animfn=station_s2bay1)
	subobject(spacestation_door, door2, v(0,1,0), v(0,0,1), scale=1.0)

	subobject(spacestation_entry1_stage3, v(0,0,0), v(0,1,0), v(0,0,1), scale=1.0, onflag=station_s2bay1)
}

model(spacestation_entry1_stage1, scale=1.0, radius=1.0) {
	a = v(-100,0,50)
	b = v(100,0,50)
	c = v(100,0,-50)
	d = v(-100,0,-50)
	a2 = v(-100,-300,50)
	b2 = v(100,-300,50)
	c2 = v(100,-300,-50)
	d2 = v(-100,-300,-50)
	
	material(.5,.5,.5, 0,0,0, 0, 0,0,0)
	xref:quad(b,c,c2,b2)
	quad(a,b,b2,a2)
	quad(c,d,d2,c2)
}

model(spacestation_entry1, scale=1.0, radius=1.0) {

	material(.2,.2,.6, 0,0,0, 0, 0,0,0)
	door1 = vlinear(v(0,-20,0), v(0,-20,-100), animfn=station_s1bay1)
	subobject(spacestation_door, door1, v(0,1,0), v(0,0,1), scale=1.0)
	
	subobject(spacestation_entry1_stage1, v(0,0,0), v(0,1,0), v(0,0,1), scale=1.0, onflag=station_s1bay1)
	# docking surface
	geomflag(0x10)
	invisible:quad(v(-100,-100,50), v(100,-100,50), v(100,-100,-50), v(-100,-100,-50))
	geomflag(0x8000)
	invisible:tri(v(0,-150,0), v(0,0,0), v(0,0,0))
	invisible:tri(v(0,0,0), v(-1,0,0), v(0,0,-1))
	geomflag(0)

	subobject(spacestation_entry1_stage2, v(0,-300,0), v(0,1,0), v(0,0,1), scale=1.0)

}

model(nice_spacestation, scale=1.0, radius=500.0) {
	# front
	# f7   f3    f6
	#
	#    a b
	# f4 d c   f2
	#
	# f8  f1    f5
	a = v(-100,400,50)
	b = v(100,400,50)
	c = v(100,400,-50)
	d = v(-100,400,-50)
	
	f1 = v(0,400,-400)
	f2 = v(400,400,0)
	f3 = v(0,400,400)
	f4 = v(-400,400,0)

	f5 = v(400,0,-400)
	f6 = v(400,0,400)
	f7 = v(-400,0,400)
	f8 = v(-400,0,-400)

	# back face points
	f1b = v(0,-400,-400)
	f2b = v(400,-400,0)
	f3b = v(0,-400,400)
	f4b = v(-400,-400,0)
	
	material(1.0,0.5,1.0, 0,0,0, 0, 0,0,0)
	textpos = v(0,400,-80)
	zbias(textpos,v(0,1,0),1)
	text(params:0, textpos, v(0,1,0), v(1,0,0), scale=10.0)
	nozbias()

	material(.5,.5,.5, 0,0,0, 0, 0,0,0)
	#front face
	tri(f1,d,c)
	xref:tri(f1,c,f2)
	xref:tri(f2,c,b)
	xref:tri(f2,b,f3)
	tri(f3,b,a)
	#pyramid sides
	xref:tri(f2,f3,f6)
	xref:tri(f5,f1,f2)
	xref:tri(f6,f3b,f2b)
	xref:tri(f5,f2b,f1b)
	xref:quad(f2,f6,f2b,f5) # sides
	quad(f3,f7,f3b,f6) # top
	quad(f5,f1b,f8,f1) # bottom
	quad(f1b,f2b,f3b,f4b) # rear
	subobject(spacestation_entry1, v(0,400,0), v(0,1,0), v(0,0,1), scale=1.0)
}

model(basic_groundstation, scale=1.0, radius=1.0) {
	material(.5,.5,.5, 0,0,0, 0, 0,0,0)
	# surface recognised for landing on (bay 1)
	geomflag(0x10)
	extrusion(v(0,0,-50), v(0,0,50), n(0,1,0), radius=1.0, {
		v(-50,0,0), v(50,0,0), v(50,10,0), v(-50,10,0)
	})
	geomflag(0x11)
	extrusion(v(0,0,70), v(0,0,170), n(0,1,0), radius=1.0, {
		v(-50,0,0), v(50,0,0), v(50,10,0), v(-50,10,0)
	})
	bay1top = v(0,10,0)
	bay2top = v(0,10,120)
	# docking bay 1 location,xaxis,yaxis
	geomflag(0x8000)
	invisible:tri(bay1top, v(0,0,0), v(0,0,0))
	invisible:tri(v(0,0,0), v(1,0,0), v(0,1,0))
	geomflag(0x8001)
	invisible:tri(bay2top, v(1,0,0), v(0,1,0))
	geomflag(0)
	material(1,1,1, 0,0,0, 0, 0,0,0)
	zbias(bay1top, v(0,1,0), 1)
	text(global:10, bay1top, v(0,1,0), v(1,0,0), scale=20.0)
	zbias(bay2top, v(0,1,0), 1)
	text(global:11, bay2top, v(0,1,0), v(1,0,0), scale=20.0)
	nozbias()
}

#model(hm, scale=1.0, radius=1.0) {
#	a = v(-100,0,-100)
#	b = v(100,0,-100)
#	c = v(100,0,100)
#	d = v(-100,0,100)
#	a2 = v(-100,50,-100)
#	b2 = v(100,50,-100)
#	c2 = v(100,50,100)
#	d2 = v(-100,50,100)
#	# a b
#	# c d
#	
#
#
#}

